// 这里添加下面两行代码。
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

// 定义两个链接，下面会用到。
def siteUrl = 'https://github.com/ruiAndroid/alphaplayer' // 项目主页。
def gitUrl = 'https://github.com/ruiAndroid/alphaplayer.git' // Git仓库的url。

// 唯一包名，比如implementation 'com.rui:alphaplayer:1.0.0'中的com.rui就是这里配置的。
group = "com.rui"

//项目引用的版本号，比如implementation 'com.rui:alphaplayer:1.0.0'中的2.1.0就是这里配置的。
version = "1.0.6"

install {
    repositories.mavenInstaller {
        // 生成pom.xml和参数
        pom {
            project {
                packaging 'aar'
                // 项目描述，复制我的话，这里需要修改。
                name 'alphaplayer'// 可选，项目名称。
                description 'can play alpha video'// 可选，项目描述。
                url siteUrl // 项目主页，这里是引用上面定义好。

                // 软件开源协议，现在一般都是Apache License2.0吧，复制我的，这里不需要修改。
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                //填写开发者基本信息，复制我的，这里需要修改。
                developers {
                    developer {
                        id 'jr' // 开发者的id。
                        name 'jianrui' // 开发者名字。
                        email 'rui940718@gmail.com' // 开发者邮箱。
                    }
                }

                // SCM，复制我的，这里不需要修改。
                scm {
                    connection gitUrl // Git仓库地址。
                    developerConnection gitUrl // Git仓库地址。
                    url siteUrl // 项目主页。
                }
            }
        }
    }

//    repositories.mavenDeployer{
//        // Ubuntu本地仓库路径， Windows 为(url:"file://D://***/***/***/")
////        repository(url:"file:/home/jinyx/develop/other/")
//        repository(url:"file://C://Users/jianrui/Desktop/")
//        // 唯一标识
//        pom.groupId = "com.rui"
//        // 项目名称
//        pom.artifactId = "alphaPlayer"
//        // 版本号
//        pom.version = "1.0"
//    }
}

// 生成jar包的task，不需要修改。
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

// 生成jarDoc的task，不需要修改。
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    // destinationDir = file("../javadoc/")
    failOnError false // 忽略注释语法错误，如果用jdk1.8你的注释写的不规范就编译不过。
}

// 生成javaDoc的jar，不需要修改。
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

// 这里是读取Bintray相关的信息，我们上传项目到github上的时候会把gradle文件传上去，所以不要把帐号密码的信息直接写在这里，写在local.properties中，这里动态读取。
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty("bintray.user") // Bintray的用户名。
    key = properties.getProperty("bintray.apikey") // Bintray刚才保存的ApiKey。

    configurations = ['archives']
    pkg {
        repo = "maven"  //Repository名字 需要自己在bintray网站上先添加
        name = "alphaPlayer"// 发布到Bintray上的项目名字，这里的名字不是implementation 'com.rui:alphaplayer:1.0.0'中的alphaplayer。
        userOrg = 'rui940718'//Bintray的组织id
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true // 是否是公开项目。
    }
}